start: type_decls program_decl func_decls

type_decls: type_decl*

?type_decl: enum_decl
  | enum_set_decl
  | value_decl

enum_decl: "enum" type_name enum_body

enum_set_decl: "enumset" type_name "[" INT "]" enum_body

?enum_body: ";"
  | "{" enum_items "}"

enum_items: enum_item ("," enum_item)*

value_decl: "value" type_name ";"

program_decl: "program" func_name "(" type_names ")" "->" type_name ";"

func_decls: func_decl*

func_decl: "func" func_name ":" func_body ";"

func_body: func_lhs "->" func_rhss

?func_lhs: opt_arg

func_rhss: func_rhs ("," func_rhs)*

?func_rhs: opt_arg

opt_arg: type_name [var_name]

type_names: [type_name ("," type_name)*]

?enum_item: STRLIT

?type_name: NAME
?var_name: NAME
?func_name: NAME

COMMENT: "#" /.*/ "\n"
  | "(*" /(.|\n)+/ "*)"

%import common.ESCAPED_STRING -> STRLIT
%import common.CNAME -> NAME
%import common.INT -> INT
%import common.WS
%ignore WS
%ignore COMMENT